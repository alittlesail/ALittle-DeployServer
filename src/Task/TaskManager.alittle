
namespace DeployServer;

protected var g_LJobWorker:ALittle.IWorkerCommon;
protected var g_HJobWorker:ALittle.IWorkerCommon;

public class TaskManager
{
    private int _max_task_id = 0;

    private Map<int, Task> _task_map = new Map<int, Task>();

    public await fun Setup()
    {
        var error = A_MysqlSystem.CreateIfNotExit{TaskInfo}();
        assert(error == null, error);
        error = A_MysqlSystem.CreateIfNotExit{BuildInfo}();
        assert(error == null, error);

        var task_error, task_list = A_MysqlSystem.SelectListFromByMap(new TaskInfo());
        assert(task_error == null, task_error);

        // 开始构建任务列表
        for (var index, task_info in task_list)
        {
            var build_info = new BuildInfo();
            build_info.task_id = task_info.task_id;
            var build_error, build_list = A_MysqlSystem.SelectListFromByMap(build_info);
            if (build_list == null) build_list = new List<BuildInfo>();
            this._task_map[task_info.task_id] = new Task(task_info, build_list);
            if (this._max_task_id < task_info.task_id) this._max_task_id = task_info.task_id;
        }

        A_WebAccountManager.AddEventListener(this, this.HandleAccountLogin);

        g_LJobWorker = new ALittle.Worker(g_ModuleScriptPath.."Task/Workers/JobWorker");
        g_HJobWorker = new ALittle.Worker(g_ModuleScriptPath.."Task/Workers/JobWorker");
    }

    private fun HandleAccountLogin(ALittle.WebAccountLoginEvent event)
    {
        var info = new S2CTaskList();
        info.task_list = new List<D_TaskInfo>();
        for (var id, task in this._task_map)
            ALittle.List_Push(info.task_list, task.data_info);
        event.account.SendMsg(info);
    }

    public await fun HandleCreateTask(string task_name)
    {
        ++this._max_task_id;
        var task_info = new TaskInfo();
        task_info.task_id = this._max_task_id;
        task_info.task_name = task_name;
        task_info.create_time = ALittle.Time_GetCurTime();
        task_info.job_list = new List<JobInfo>();
        var error = A_MysqlSystem.InsertInto(task_info);
        assert(error == null, error);

        var build_info = new BuildInfo();
        build_info.task_id = task_info.task_id;
        var build_error, build_list = A_MysqlSystem.SelectListFromByMap(build_info);
        if (build_list == null) build_list = new List<BuildInfo>();

        // 创建任务
        var task = new Task(task_info, build_list);
        this._task_map[task_info.task_id] = task;

        // 将消息发给所有账号
        var msg = new NCreateTask();
        msg.task_info = task.data_info;
        A_WebAccountManager.SendMsgToAll(msg);
    }

    public await fun HandleCopyTask(int task_id)
    {
        var target = this._task_map[task_id];
        assert (target != null, "要复制的任务不存在");

        ++this._max_task_id;
        var task_info = ALittle.String_CopyTable(target.info);
        task_info.task_id = this._max_task_id;
        task_info.create_time = ALittle.Time_GetCurTime();
        var error = A_MysqlSystem.InsertInto(task_info);
        assert(error == null, error);

        var build_info = new BuildInfo();
        build_info.task_id = task_info.task_id;
        var build_error, build_list = A_MysqlSystem.SelectListFromByMap(build_info);
        if (build_list == null) build_list = new List<BuildInfo>();

        // 创建任务
        var task = new Task(task_info, build_list);
        this._task_map[task_info.task_id] = task;

        // 将消息发给所有账号
        var msg = new NCreateTask();
        msg.task_info = task.data_info;
        A_WebAccountManager.SendMsgToAll(msg);
    }

    public await fun HandleDeleteTask(int task_id)
    {
        var task = this._task_map[task_id];
        assert (task != null, "任务不存在");
        assert (task.status == TaskStatus.IDLE, "任务不在空闲状态");

        var error = A_MysqlSystem.DeleteFromByKey{TaskInfo}("task_id", task_id);
        assert(error == null, error);

        task.HandleDelete();
        this._task_map[task_id] = null;

        // 将消息发给所有账号
        var msg = new NDeleteTask();
        msg.task_id = task_id;
        A_WebAccountManager.SendMsgToAll(msg);
    }

    public fun GetTask(int task_id) : Task { return this._task_map[task_id]; }

    public fun StartTaskByWebHook(string url)
    {
        for (var index, task in this._task_map)
            task.StartByWebHook(url);
    }

    public fun Shutdown()
    {
        if (g_LJobWorker != null)
        {
            g_LJobWorker.Stop();
            g_LJobWorker = null;
        }

        if (g_HJobWorker != null)
        {
            g_HJobWorker.Stop();
            g_HJobWorker = null;
        }
    }
}

public var g_TaskManager = new TaskManager();


public struct S2CTaskList
{
    List<D_TaskInfo> task_list;
}

public struct C2SCreateTask
{
    string task_name;
}

public struct S2CCreateTask
{
}

public struct NCreateTask
{
    D_TaskInfo task_info;
}

[Msg]
public await static HandleC2SCreateTask(ALittle.IMsgCommon sender, C2SCreateTask msg) : S2CCreateTask
{
    A_WebAccountManager.CheckLoginByClient(sender);
    g_TaskManager.HandleCreateTask(msg.task_name);
    return new S2CCreateTask();
}

public struct C2SCopyTask
{
    int task_id;
}

public struct S2CCopyTask
{

}

[Msg]
public await static HandleC2SCopyTask(ALittle.IMsgCommon sender, C2SCopyTask msg) : S2CCopyTask
{
    A_WebAccountManager.CheckLoginByClient(sender);
    g_TaskManager.HandleCopyTask(msg.task_id);
    return new S2CCopyTask();
}

public struct C2SDeleteTask
{
    int task_id;
}

public struct S2CDeleteTask
{

}

public struct NDeleteTask
{
    int task_id;
}

[Msg]
public await static HandleC2SDeleteTask(ALittle.IMsgCommon sender, C2SDeleteTask msg) : S2CDeleteTask
{
    A_WebAccountManager.CheckLoginByClient(sender);
    g_TaskManager.HandleDeleteTask(msg.task_id);
    return new S2CDeleteTask();
}